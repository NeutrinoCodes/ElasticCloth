/// @file
#version 460 core

uniform mat4 V_mat;                                                             // View matrix.
uniform mat4 P_mat;                                                             // Projection matrix.
uniform float size_x;                                                           // Framebuffer size_x.
uniform float size_y;                                                           // Framebuffer size_y.
uniform float AR;                                                               // Framebuffer aspect ratio.

layout (points) in;                                                             // Input points.
layout (triangle_strip, max_vertices = 64) out;                                 // Output points.

// Voxel colors:
layout(std430, binding = 0) buffer voxel_color
{
  vec4 color_SSBO[];                                                            // Voxel color SSBO.
};

// Voxel centers:
layout(std430, binding = 1) buffer voxel_center
{
  vec4 center_SSBO[];                                                           // Voxel center SSBO.
};

// Voxel nearest neighbours:
layout(std430, binding = 11) buffer voxel_nearest
{
  int nearest_SSBO[];                                                           // Voxel nearest SSBO.
};

// Voxel offsets:
layout(std430, binding = 12) buffer voxel_offset
{
  int offset_SSBO[];                                                            // Voxel offset SSBO.
};

out vec4 color;
out vec2 quad;
out float s;
out float AR_quad;

void main()
{
  uint i = gl_PrimitiveIDIn;                                                    // Central node index.        
  uint j = 0;                                                                   // Offset index.
  uint j_min = 0;                                                               // Neighbour node minimum index.
  uint j_max = offset_SSBO[i];                                                  // Neighbour node maximum index.
  uint k = 0;                                                                   // Neighbour node index.

  vec4 A;
  vec4 B;
  vec4 C;
  vec4 D;

  vec4 a;
  vec4 b;
  vec4 c;
  vec4 d;
  vec4 e;
  vec4 f;

  mat2 M;
  
  vec2 link;
  vec4 P;
  vec4 Q;

  float base;
  float height;

  s = 0.02;                                                                     // Setting billboard height (in clip space)...

  // FINDING MINIMUM STRIDE INDEX:
  if (i == 0)
  {
    j_min = 0;                                                                  // Setting stride minimum (first stride)...
  }
  else
  {
    j_min = offset_SSBO[i - 1];                                                 // Setting stride minimum (all others)...
  }

  // BUILDING LINE FROM CENTER TO NEIGHBOUR:
  for (j = j_min; j < j_max; j++)
  {
    k = nearest_SSBO[j];                                                        // Computing neighbour index...
    
    // COMPUTING BILLBOARD ROTATION:
    P = P_mat*V_mat*center_SSBO[i];                                             // Getting center node (in clip space)...
    Q = P_mat*V_mat*center_SSBO[k];                                             // Getting neighbour node (in clip space)...
    link = normalize(vec2(AR*(Q.x/Q.w - P.x/P.w), (Q.y/Q.w - P.y/P.w)));        // Computing normalized PQ segment (in window space)...
    M[0][0] = +link.x; M[0][1] = +link.y;                                       // Computing rotation matrix (in window space)...
    M[1][0] = -link.y; M[1][1] = +link.x;                                       // Computing rotation matrix (in window space)...                                                                  
    A = s*vec4(-0.5, +0.5, 0.0, 1.0);                                           // Setting billboard vertex "a" (in clip space)...
    B = s*vec4(-0.5, -0.5, 0.0, 1.0);                                           // Setting billboard vertex "b" (in clip space)...
    C = s*vec4(+0.5, +0.5, 0.0, 1.0);                                           // Setting billboard vertex "c" (in clip space)...
    D = s*vec4(+0.5, -0.5, 0.0, 1.0);                                           // Setting billboard vertex "d" (in clip space)...
    A.xy = M*A.xy;                                                              // Rotating billboard vertex according to PQ segment (in window space)...                                         
    B.xy = M*B.xy;                                                              // Rotating billboard vertex according to PQ segment (in window space)...
    C.xy = M*C.xy;                                                              // Rotating billboard vertex according to PQ segment (in window space)...
    D.xy = M*D.xy;                                                              // Rotating billboard vertex according to PQ segment (in window space)...
    
    // COMPUTING BILLBOARD ASPECT RATIO:
    a = vec4(P_mat*(V_mat*center_SSBO[i] + A));                                 // Computing billboard boundary "a" (in clip space)...
    b = vec4(P_mat*(V_mat*center_SSBO[i] + B));                                 // Computing billboard boundary "b" (in clip space)...
    c = vec4(P_mat*(V_mat*center_SSBO[k] + C));                                 // Computing billboard boundary "c" (in clip space)...
    d = vec4(P_mat*(V_mat*center_SSBO[k] + D));                                 // Computing billboard boundary "d" (in clip space)...
    e = vec4(P_mat*(V_mat*center_SSBO[i] + 0.5*(A + B)));                       // Computing billboard "ab" midpoint (in clip space)...
    f = vec4(P_mat*(V_mat*center_SSBO[k] + 0.5*(C + D)));                       // Computing billboard "cd" midpoint (in clip space)...
    height = length(vec2(AR*(b.x/b.w - a.x/a.w), (b.y/b.w - a.y/a.w)));         // Computing billboard height (in window space)...
    base = length(vec2(AR*(f.x/f.w - e.x/e.w), (f.y/f.w - e.y/e.w)));           // Computing billboard base (in window space)...
    AR_quad = base/height;                                                      // Computing bollboard aspect ratio (in window space)...

    // GENERATING BILLBOARD VERTICES:
    color = color_SSBO[i];                                                      // Setting voxel color...  
    gl_Position = a;                                                            // Setting billboard vertex "a"...
    quad = vec2(-0.5*AR_quad, +0.5);                                            // Setting quad vertex (in UV space)...
    EmitVertex();                                                               // Emitting vertex...

    color = color_SSBO[i];                                                      // Setting voxel color...
    gl_Position = b;                                                            // Setting billboard vertex "b"...
    quad = vec2(-0.5*AR_quad, -0.5);                                            // Setting quad vertex (in UV space)...
    EmitVertex();                                                               // Emitting vertex...

    color = color_SSBO[i];                                                      // Setting voxel color...  
    gl_Position = c;                                                            // Setting billboard vertex "c"...
    quad = vec2(+0.5*AR_quad, +0.5);                                            // Setting quad vertex (in UV space)...
    EmitVertex();                                                               // Emitting vertex...

    color = color_SSBO[i];                                                      // Setting voxel color...  
    gl_Position = d;                                                            // Setting billboard vertex "d"...
    quad = vec2(+0.5*AR_quad, -0.5);                                            // Setting quad vertex (in UV space)...
    EmitVertex();                                                               // Emitting vertex...

    EndPrimitive();                                                             // Ending primitive...
  }
}
